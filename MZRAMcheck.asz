;MZ-80BのRAMをチェックする
;2716などに書き込んで、IPLのROMと取り換えて起動

	org	0x0000

START:	
	ld	a,0x82		;8255 A=OUT B=IN C=OUT
	out	(0xE3),a
	ld	a,0x0F		;PIO A=OUT
	out	(0xE9),a
	ld 	a,0xCF		;PIO B=IN
	out	(0xEB),a
	ld	a,0xFF
	out	(0xEB),a
	ld	a,0x58		;BST=1 OPEN=1 WRITE=1
	out	(0xE2),a
	ld	a,0x12
	out	(0xE0),a	; CRT normal1, BLK1 set FF1, 
	xor	a	
	out	(0xF4),a	; GVRAM 1 write, no display, GVRAM 2 no write , no display

	ld iy,LS
	jp CLS
;bank I,II check $0000-$7FFF($8000-$FFFF)

LS:
	xor a
	inc a
L0:
	ex af,af'
	ld hl,0x7FFF	; check add
	ld bc,0x8000	; loop count
	dec bc
	inc c
	inc b
	ld a,c
	ld c,b
	ld b,a
	ex af,af'
L1:
	inc hl
	ld (hl),a
	ld ix,CHK
	jr PUTAD
CHK:
	cp (hl)
	jr nz,ERR
RL1:
	djnz L1
	dec c
	jr nz,L1
	rlca
	jr nc,L0

; restart NST
NST:
	ld hl,0x0000
	ld de,0x8000
	ld bc,0x150
	ldir
	ld a,02
	ld (0x8001),a
	xor a
	ld (0x8000+SUB),a
	ld (0x8001+SUB),a
	ld a,0x76
	ld (0x8000+NST),a
	ld a,0x03
	out	(0E3H),a
	halt

; print address and data
PUTAD:
	ex af,af'
	ld a,0x90
	out (0xE8),a

	ld a,h
SUB:
	sub 0x80

	exx
	ld bc,0xD000
	ld iy,RTNAD
	jr BTOC
RTNAD:
	exx
	ld a,l
	exx
	ld iy,RTNAD1
	jr BTOC
RTNAD1:
	ld a,0x2D
	ld (bc),a
	inc bc
	ex af,af'
	exx
	ld d,a
	exx
	ld iy,RTNAD2
	jr BTOC
RTNAD2:
	ld a,0x2D
	ld (bc),a
	inc bc
	ld a,0x10
	out (0xE8),a	
	exx
	ld a,d
	jp (ix)
	halt

;	print error data and message
ERR:
	ex af,af'
	ld a,0x90
	out (0xE8),a
	ld a,(hl)
	exx
	ld iy,PUTER
	jr BTOC
PUTER:
	ld a,0x2D
	ld (bc),a
	inc bc
	ld hl,ERST
PUTST:
	ld a,(hl)
	cp 0x0
	jr nz,PUTCH
;wait key in
	in a,(0xE8)
	and 0xF0
	or 0x13
	out (0xE8),a
KIN:
	in a,(0xEA)
	and 0x02
	jr nz,KIN
WAIT:
	in a,(0xEA)
	and 0x02
	jr z,WAIT

	ld a,0x10
	out (0xE8),a
	ld iy,BCK
	jr CLS
BCK:
	ex af,af'
	exx
	jp RL1

PUTCH:
	ld (bc),a
	inc bc
	inc hl
	jr PUTST
		
BTOC:
; A(hex8bit)を文字列へDEへ（D下、E上)
	ld e,a
	and 0x0F
	add a,0x30
	cp 0x3A
	jr c,STRD
	add a,0x07
STRD:
	ld d,a

	ld a,e
	and 0xF0
	rrca
	rrca
	rrca
	rrca
	add a,0x30
	cp 0x3A
	jr c,STRE
	add a,0x07
STRE:
	ld e,a

	ld a,e
	ld (bc),a
	inc bc
	ld a,d
	ld (bc),a
	inc bc
	jp (iy)
 

CLS:
;clear Character display
	ld	hl,0xD000
	ld	a,0xB3
	out	(0xE8),a
CLEAR:	
	ld	(hl),0x00	;DISPLAY CLEAR
	inc	hl
	ld	a,h
	or	l
	jr	nz,CLEAR
	ld	a,0x13
	out	(0xE8),a
	jp (iy)

ERST:
	DB "ER"
	DB 0x00